plugins {
    id "com.android.library"
    id "kotlin-android"
}

group 'com.piccmaq.disk_space_plus'
version '1.0-SNAPSHOT'

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new RuntimeException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "$flutterRoot/bin/cache/artifacts/engine/android-arm"
        }
        maven {
            url "$flutterRoot/bin/cache/artifacts/engine/android-arm64"
        }
        maven {
            url "$flutterRoot/bin/cache/artifacts/engine/android-x64"
        }
    }
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    namespace 'com.piccmaq.disk_space_plus'

    compileSdkVersion safeExtGet('compileSdkVersion', 34)

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    lintOptions {
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.10"
    compileOnly files("$flutterRoot/bin/cache/artifacts/engine/android-arm/flutter.jar")
}
